PREFIX graph: <http://linkeddata.crow.nl/imbor/def/graph/>
PREFIX groep: <http://linkeddata.crow.nl/imbor/def/groepering/>
PREFIX imbor: <http://linkeddata.crow.nl/imbor/def/objecttype/>
PREFIX imborp: <http://linkeddata.crow.nl/imbor/def/eigenschap/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX shape: <http://linkeddata.crow.nl/imbor/def/shape/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#>

# Deze query maakt eigenschappen/relaties/attributen in de Eigenschappen en in de
# Shapes grafen. Ook bijbehorende Attribuutsorteergroepen, sh:PropertyGroups
# worden hierbij gemaakt.


INSERT {
    GRAPH graph:GUID {
        ?attr skos:notation ?guid .
    }
    
    GRAPH graph:Eigenschappen {
        ?attr
            a rdf:Property ;  # niet definitief
            rdfs:label ?label ;
            skos:definition ?definition ;
            skos:editorialNote ?editorialNote ;
            skos:altLabel ?altLabel ;
            .
    }

    GRAPH graph:Shapes {
        ?shape
            a sh:PropertyShape ;
            sh:path ?attr ;
            sh:group ?attribuutsorteergroep ;
            .

        ?attribuutsorteergroep
            a sh:PropertyGroup ;
            sh:order ?order ;
            rdfs:label ?sorteergroepLabel ;
            .
    }
}

WHERE {
    GRAPH graph:X_Attributen {
        ?attr
            imborp:guid ?guid ;
            rdfs:label ?label ;
            imborp:attribuutsorteergroep ?attribuutsorteergroep ;
            imborp:informatiemodel ?informatiemodel ;
            .

        OPTIONAL { ?attr skos:altLabel ?altLabel . }
        OPTIONAL { ?attr skos:definition ?definition . }
        OPTIONAL { ?attr skos:editorialNote ?editorialNote . }
    }

    FILTER ( ?informatiemodel = groep:INF18 )  # Alleen IMBOR wordt meegenomen

    BIND(
        URI(
            CONCAT(
                REPLACE( STR(?attr), STR(imborp:), STR(shape:) ),
                "Shape"
            )
        )
        AS ?shape
    )

    GRAPH graph:X_Attribuutsorteergroep {
        ?attribuutsorteergroep
            rdfs:label ?sorteergroepLabel ;
            imborp:volgorde ?volgorde ;
            .
    }

    BIND ( xsd:integer(?volgorde) AS ?order )
}
