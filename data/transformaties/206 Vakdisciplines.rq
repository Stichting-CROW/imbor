PREFIX graph: <http://linkeddata.crow.nl/imbor/def/graph/>
PREFIX groep: <http://linkeddata.crow.nl/imbor/def/groepering/>
PREFIX imbor: <http://linkeddata.crow.nl/imbor/def/objecttype/>
PREFIX imborp: <http://linkeddata.crow.nl/imbor/def/eigenschap/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX shape: <http://linkeddata.crow.nl/imbor/def/shape/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>


INSERT {
    GRAPH graph:Groeperingen {
        groep:IMBORVakdisciplineCollectie
            a skos:Collection ;
            rdfs:label "IMBOR Vakdisciplines" ;
            skos:member ?vakdiscipline ;
            .

        ?vakdiscipline
            a skos:Collection ;
            rdfs:label ?label ;
            rdfs:comment ?definitie ;
            skos:member ?ot ;
            .
    }
}

WHERE {
    GRAPH graph:X_Vakdisciplines {
        ?vakdiscipline
            rdfs:label ?label ;
            skos:definition ?definitie ;
            imborp:informatiemodel ?informatiemodel ;
            .
    }

    GRAPH graph:X_VakdisciplinesObjecttype {
        ?vo
            imborp:vakdiscipline ?vakdiscipline ;
            imborp:objecttype ?ot ;
            .
    }

    FILTER ( ?informatiemodel = groep:INF18 )  # Alleen IMBOR wordt meegenomen
}