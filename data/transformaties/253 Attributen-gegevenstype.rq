PREFIX graph: <http://linkeddata.crow.nl/imbor/id/graph/>
PREFIX groep: <http://linkeddata.crow.nl/imbor/id/groepering/>
PREFIX imbor: <http://linkeddata.crow.nl/imbor/id/objecttype/>
PREFIX imborp: <http://linkeddata.crow.nl/imbor/id/eigenschap/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX shape: <http://linkeddata.crow.nl/imbor/id/shape/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#>
PREFIX nta8035: <https://w3id.org/def/basicsemantics-owl#>
PREFIX qudt: <http://qudt.org/vocab/quantitykind/>
PREFIX unit: <http://qudt.org/vocab/unit/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>


INSERT {
    GRAPH graph:Shapes {
        ?shape
            sh:datatype ?unit ;
            .
    }

    GRAPH graph:GUID {
        ?unit skos:notation ?guid .
    }
}

WHERE {
    GRAPH graph:X_Attributen {
        ?attr imborp:gegevensType ?datatype .

        # deze query voegt eenheden toe waar die niet uit imborp:eenheid komen
        FILTER NOT EXISTS { ?attr imborp:eenheid ?eenheid . }
    }

    GRAPH graph:X_Gegevenstypen {
        ?datatype
            imborp:guid ?guid ;
            rdfs:label ?label ;
            .
    }

    
    GRAPH graph:Shapes {
        ?shape
            a sh:PropertyShape ;
            sh:path ?attr ;
            .
    }

    VALUES (?guid ?unit) {
        ( <urn:uuid:7DA44BF0-3BBE-4DF6-966F-8DA88755821C> xsd:positiveInteger ) # Autonum; 0 inst
        ( <urn:uuid:6473DA75-410E-425A-BDB4-54C6DEBC8B20> xsd:string ) # Tekst
        ( <urn:uuid:C6B46F31-ADEE-42AD-AE9D-83CEC3F115C9> xsd:string ) # Memo
        ( <urn:uuid:32971785-49DF-4A63-9F3A-24C54645C311> xsd:decimal ) # Numeriek
        ( <urn:uuid:9D0F8D13-3B07-4AA3-9B37-463D303E6D26> xsd:date ) # Datum
        ( <urn:uuid:A7EA5E5D-EAAF-4791-A729-7FE8109FB617> unit:Euro ) # Valuta
        ( <urn:uuid:483B69DC-6F47-4753-83F8-0DAD1F89D4CA> xsd:anyURI ) # Hyperlink; 0 inst
        ( <urn:uuid:71DE8A90-1A4A-4D96-A85F-4F2B261823EA> xsd:anyURI ) # Ole-object
        ( <urn:uuid:04DA3D22-E426-427C-8A11-6DF073DE492C> xsd:boolean ) # Boolean
        ( <urn:uuid:6D3F48A5-4C29-4509-9E49-5750C2AAC278> xsd:string ) # Bijlage; 0 inst
        ( <urn:uuid:624F55A1-A4AC-45F0-B2B5-A62AB9718B81> xsd:string ) # Berekend; 0 inst
        ( <urn:uuid:A9519639-348C-40C3-AFAE-A89C1ED317E2> geo:wktLiteral ) # Spatial
        ( <urn:uuid:3F6FA377-234D-48E7-9742-FE5FEFEBA120> xsd:string ) # GUID
        ( <urn:uuid:F39307C6-1243-424D-9858-4469FDE3889E> xsd:dateTime ) # DatumTijd
        ( <urn:uuid:EFC82703-0417-4871-8BD9-6238D5F5CD1C> xsd:string ) # Samengesteld (tuple)
        ( <urn:uuid:46574148-D0A8-4D9D-BD70-6C1C156F8A9D> geo:wktLiteral ) # Co√∂rdinaat    
    }
}