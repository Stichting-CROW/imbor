PREFIX graph: <http://linkeddata.crow.nl/publication-v2/ns/crow/imbor/def/graph/>
PREFIX groep: <http://linkeddata.crow.nl/publication-v2/ns/crow/imbor/def/groepering/>
PREFIX imbor: <http://linkeddata.crow.nl/publication-v2/ns/crow/imbor/def/objecttype/>
PREFIX imborp: <http://linkeddata.crow.nl/publication-v2/ns/crow/imbor/def/eigenschap/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX shape: <http://linkeddata.crow.nl/publication-v2/ns/crow/imbor/def/shape/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX nta8035: <https://w3id.org/def/basicsemantics-owl#>


# X_Objecttype ObjecttypeNiveau1 is
# 1. 'Beheerobject'
# 2. 'Groenobject/Sluis/Wegas' voor de raakvlakobjecten
# Daarom worden de objecttypes in drie queries toegevoegd: 
# 1 maal voor algemene eigenschappen, 1 maal voor beheerobjecten, 1 maal voor raakvlakobjecten 

INSERT {
    GRAPH graph:GUID {
        ?ot skos:notation ?guid .
    }

    GRAPH graph:Objecten {
        ?ot a rdfs:Class ;
            rdfs:label ?label ;
            skos:altLabel ?altLabel ;
            skos:definition ?definition ;
            skos:editorialNote ?editorialNote ;
            .
    }

    GRAPH graph:Groeperingen {
        groep:Objecttype
            a skos:Collection ;
            rdfs:label "IMBOR ObjecttypehiÃ«rarchie: Objecttypes" ;
            skos:member ?ot ;
        .

        ?objecttypegroep skos:member ?ot .
    }
}

WHERE {
    GRAPH graph:X_Objecttypes {
        ?ot
            rdfs:label ?label ;
            imborp:guid ?guid ;
            
            imborp:informatiemodel ?informatiemodel ;
        .

        OPTIONAL { ?ot skos:altLabel ?altLabel . }
        OPTIONAL { ?ot skos:definition ?definition . }
        OPTIONAL { ?ot skos:editorialNote ?editorialNote . }

        OPTIONAL { ?ot imborp:objecttypegroep ?objecttypegroep . }
    }

    FILTER ( ?informatiemodel = groep:INF18 )  # Alleen IMBOR wordt meegenomen
}

# # #                # # #
; # Raakvlakobject = FALSE
# # #                # # #

INSERT {
    GRAPH graph:Objecten {
        ?ot rdfs:subClassOf ?hoofdobject .
        
        ?hoofdobject rdfs:subClassOf ?beheerobject .

        ?beheerobject rdfs:subClassOf nta8035:PhysicalObject .
    }
}

WHERE {
    GRAPH graph:X_Objecttypes {
        ?ot
            imborp:isRaakvlakobject false ;
            imborp:informatiemodel ?informatiemodel ;
            imborp:ObjecttypeNiveau1 ?beheerobject ; # altijd aanwezig bij IMBOR-objecttypen
            .

        OPTIONAL { ?ot imborp:ObjecttypeNiveau2 ?hoofdobjecttype . }  # hoofdobject v/e objecttypegroep
    }

    FILTER ( ?informatiemodel = groep:INF18 )  # Alleen IMBOR wordt meegenomen

    # Om de subclassehierarchie goed te doen, is het ?tussenliggendOT soms niet aanwezig. 
    # als ?hoofdobjecttype = NULL -> zelf ot = hoofdobjecttype
    BIND(
        COALESCE(?hoofdobjecttype, ?ot)
        AS
        ?hoofdobject
    )
    # als er dan zelfsubclasses komen, ruimen we die later wel op
}