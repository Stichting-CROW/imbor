PREFIX graph: <http://linkeddata.crow.nl/imbor/def/graph/>
PREFIX groep: <http://linkeddata.crow.nl/imbor/def/groepering/>
PREFIX imbor: <http://linkeddata.crow.nl/imbor/def/objecttype/>
PREFIX imborp: <http://linkeddata.crow.nl/imbor/def/eigenschap/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX shape: <http://linkeddata.crow.nl/imbor/def/shape/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#>
PREFIX nta8035: <https://w3id.org/def/basicsemantics-owl#>
PREFIX qudt: <http://qudt.org/vocab/quantitykind/>
PREFIX unit: <http://qudt.org/vocab/unit/>


INSERT {
    GRAPH graph:Shapes {
        ?classShape
            sh:property ?propertyShape ;
            .

        ?propertyShape
            sh:maxCount ?maxcount ;
            # sh:minCount is in Shacl evenals IMBOR impliciet 0
            .
    }
}

WHERE {
    GRAPH graph:X_ObjecttypesAttributen {
        ?koppeling
            imborp:attribuut ?attr ;
            imborp:object ?ot ;
            imborp:relatie ?relatietype ;
            imborp:informatiemodel ?informatiemodel ;
            # imborp:eenheid is onbetrouwbaar
            .
    }

    FILTER ( ?informatiemodel = groep:INF18 )  # Alleen IMBOR wordt meegenomen

    # sh:maxCount van het attribuut
    GRAPH graph:X_Relaties {
        ?relatietype skos:notation ?relatienotatie .
    }

    VALUES ( ?relatienotatie ?maxcount ) {
        ( "1-1" 1 )
        ( "1-N" UNDEF )
    }

    GRAPH graph:Shapes {
        ?propertyShape
            a sh:PropertyShape ;
            sh:path ?attr ;
            .

        ?classShape
            a sh:NodeShape ;
            sh:targetClass ?ot ;
            .
    }    
}

