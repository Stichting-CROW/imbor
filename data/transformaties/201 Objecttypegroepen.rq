PREFIX graph: <http://linkeddata.crow.nl/imbor/def/graph/>
PREFIX groep: <http://linkeddata.crow.nl/imbor/def/groepering/>
PREFIX imbor: <http://linkeddata.crow.nl/imbor/def/objecttype/>
PREFIX imborp: <http://linkeddata.crow.nl/imbor/def/eigenschap/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>


INSERT {
    GRAPH graph:GUID {
        ?hoofdobject skos:notation ?groepGUID .
    }
    
    GRAPH graph:Groeperingen {
        groep:IMBORHierarchischeCollectie
            a skos:Collection ;
            rdfs:label "IMBOR Objecttypehiërarchieën" ;
            skos:member groep:Objecttypegroep ;
            skos:member groep:Objecttype ;
            skos:member groep:Type ;
            skos:member groep:TypeGedetailleerd ;
            skos:member groep:TypeExtraGedetailleerd ;
            .

        groep:Objecttypegroep
            a skos:Collection ;
            rdfs:label "IMBOR Objecttypehiërarchie: Objecttypegroepen" ;
            skos:member ?groep ;
        .

        ?groep
            a skos:Collection ;
            rdfs:label ?objecttypegroepcollectielabel ;
            skos:definition ?definition ;
            .
    }
}

WHERE {
    GRAPH graph:X_ObjecttypeGroepen {
        ?groep
            rdfs:label ?label ;
            imborp:guid ?groepGUID ;
            imborp:informatiemodel ?informatiemodel ;
            .

        OPTIONAL { ?groep imborp:hoofdobject ?hoofdobject . }
        OPTIONAL { ?groep skos:definition ?definition . }

        FILTER ( ?informatiemodel = groep:INF18 )
    }

    BIND(
        CONCAT(
            "IMBOR Objecttypegroep '",
            ?label,
            "'"
        )
        AS ?objecttypegroepcollectielabel
    )
}
