PREFIX graph: <http://linkeddata.crow.nl/imbor/def/graph/>
PREFIX groep: <http://linkeddata.crow.nl/imbor/def/groepering/>
PREFIX imbor: <http://linkeddata.crow.nl/imbor/def/objecttype/>
PREFIX imborp: <http://linkeddata.crow.nl/imbor/def/eigenschap/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX shape: <http://linkeddata.crow.nl/imbor/def/shape/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#>
PREFIX nta8035: <https://w3id.org/def/basicsemantics-owl#>
PREFIX qudt: <http://qudt.org/vocab/quantitykind/>
PREFIX unit: <http://qudt.org/vocab/unit/>
PREFIX imborvwl: <http://linkeddata.crow.nl/imbor/def/domeinwaarde/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>


INSERT {
    GRAPH graph:Domeinwaarden {
        # Elke domeinwaarde is een instantie van een type
        ?domeinwaardeType
            a rdfs:Class ;
            rdfs:subClassOf nta8035:EnumerationType ;
            rdfs:label ?classLabel ;
            .

        ?domeinwaarde
            a ?domeinwaardeType ;

            rdfs:label ?label ;
            skos:altLabel ?altLabel ;
            # imborp:bovenliggendLabel ?bovenliggendLabel ;  # #41 bovenliggend label wordt eruit gelaten
            skos:definition ?definition ;
            .
    }

    GRAPH graph:Eigenschappen {
        ?attr
            a owl:ObjectProperty ;
            .
    }

    GRAPH graph:Shapes {
        ?propertyShape
            sh:class ?domeinwaardeType
            .
    }

    GRAPH graph:GUID {
        ?domeinwaarde skos:notation ?guid .
    }

}

WHERE {
    # per attribuut (dat een refentietabel heeft => vastewaardelijst)
    GRAPH graph:X_Attributen {
        ?attr 
            imborp:heeftReferentietabel true ;
            imborp:informatiemodel ?informatiemodel ;
            .

        OPTIONAL { ?attr rdfs:label ?attrLabel . }
    }

    FILTER ( ?informatiemodel = groep:INF18 )  # Alleen IMBOR wordt meegenomen

    # Filter alvast de attributen die in IMBOR-LD als rdfs:subClassOf zijn
    # gemodelleerd.
    FILTER ( ?attr != imborp:P256 )  # Type
    FILTER ( ?attr != imborp:P257 )  # Type gedetailleerd
    FILTER ( ?attr != imborp:P760 )  # Type extra gedetailleerd

    # vind alle domeinwaardes die bij dat ?attr horen
    GRAPH graph:X_AttributenDomeinwaarden {
        ?domeinwaarde
            rdfs:label ?label ;
            imborp:guid ?guid ;

            imborp:attribuut ?attr ;
            imborp:informatiemodel ?informatiemodelAD ;
            .

        OPTIONAL { ?domeinwaarde imborp:bovenliggendLabel ?bovenliggendLabel . }
        OPTIONAL { ?domeinwaarde skos:altLabel ?altLabel . }
        OPTIONAL { ?domeinwaarde skos:definition ?definition . }
    }

    FILTER ( ?informatiemodelAD = groep:INF18 )  # Alleen IMBOR wordt meegenomen

    # vind de ?propertyShape die bij dat ?attr hoort
    GRAPH graph:Shapes {
        ?propertyShape
            a sh:PropertyShape ;
            sh:path ?attr ;
            .
    }

    # maak een URI
    # /def/eigenschap/P1234 -> /def/domeinwaarde/P1234Type
    BIND(
        URI(
            CONCAT(
                REPLACE(
                    STR(?attr), STR(imborp:), STR(imborvwl:)
                ),
                "Type"
            )
        )
        AS ?domeinwaardeType
    )

    # evt. nog een leuke leesbare titel voor de klasse
    BIND(
        CONCAT(
            "Waardes voor '",
            ?attrLabel,
            "'"
        )
        AS ?classLabel
    )
}

