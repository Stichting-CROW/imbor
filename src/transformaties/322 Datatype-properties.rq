PREFIX graph: <http://linkeddata.crow.nl/publication-v2/ns/crow/imbor/def/graph/>
PREFIX groep: <http://linkeddata.crow.nl/publication-v2/ns/crow/imbor/def/groepering/>
PREFIX imbor: <http://linkeddata.crow.nl/publication-v2/ns/crow/imbor/def/objecttype/>
PREFIX imborp: <http://linkeddata.crow.nl/publication-v2/ns/crow/imbor/def/eigenschap/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX shape: <http://linkeddata.crow.nl/publication-v2/ns/crow/imbor/def/shape/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#>
PREFIX nta8035: <https://w3id.org/def/basicsemantics-owl#>
PREFIX qudt: <http://qudt.org/vocab/quantitykind/>
PREFIX unit: <http://qudt.org/vocab/unit/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

# cf. NTA 8035 - pagina 51

DELETE {
    GRAPH graph:Shapes {
        ?propertyShape
            sh:path ?attr ;
            .
    }    
}

INSERT {
    GRAPH graph:Eigenschappen {
        ?attr
            rdfs:range nta8035:QuantityValue ;
            nta8035:unit ?unit ;  # TODO: NTA noemt alleen :unit op de anon waarde
            nta8035:quantityKind ?qkind ;
            .
    }

    GRAPH graph:Eigenschappen {
        ?attr
            a rdf:Property ;
            .
    }

    GRAPH graph:Shapes {
        ?propertyShape
            sh:datatype ?xsdstring ;
            sh:path ( ?attr rdf:value ) ;
            .
    }    

    GRAPH graph:GUID {
        ?eenheid skos:notation ?guid .
    }
}

WHERE {
    # ?attr WHERE 
    # referentietabel = NULL && eenheid != NULL
    GRAPH graph:X_Attributen {
        ?attr 
            imborp:eenheid ?eenheidURI ;
            imborp:heeftReferentietabel false ;
            imborp:informatiemodel ?informatiemodel ;
            .
    }

    FILTER ( ?informatiemodel = groep:INF18 )  # Alleen IMBOR wordt meegenomen

    GRAPH graph:X_Eenheden {
        ?eenheidURI
            imborp:guid ?guid ;

            imborp:kind ?grootheid ;
            imborp:unit ?eenheid ;
            .
    }

    BIND(
        URI(
            REPLACE(?grootheid, 'quantitykind:', STR(qudt:))
        )
        AS ?qkind
    )

    BIND(
        URI(
            REPLACE(
                REPLACE(
                    ?eenheid, 
                    'xsd:', 
                    STR(xsd:)
                ),
                'unit:', 
                STR(unit:)
            )
        )
        AS ?unit
    )

    GRAPH graph:Shapes {
        ?propertyShape
            a sh:PropertyShape ;
            sh:path ?attr ;
            .
    }    
}

